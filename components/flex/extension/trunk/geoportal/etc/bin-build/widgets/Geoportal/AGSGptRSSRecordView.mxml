<?xml version="1.0" encoding="utf-8"?>

<!--
See the NOTICE file distributed with
this work for additional information regarding copyright ownership.
Esri Inc. licenses this file to You under the Apache License, Version 2.0
(the "License"); you may not use this file except in compliance with
the License.  You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-->

<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" 
  backgroundAlpha="0.8">
<mx:Script>
  <![CDATA[
    import com.esri.gpt.finddata.*;
    import com.esri.gpt.utils.Utils;
    
    // instance variables ======================================================
    [Bindable]
    private var _record:AGSGptRSSRecord = new AGSGptRSSRecord();
    
    [Bindable]
    private var _width:Number = 375;
    
    [Bindable]
    private var _lblAddToMap:String = "Add To Map";
    
    [Bindable]
    private var _bShowAddToMap:Boolean = false;
    
    private var _xmlConfig:XML;
    
    private var _uiMainRssWidget:AGSGptRSSWidget;
    
    private var _endPoint:AGSGptRSSEndPoint;
    
    private static var _dctMapsBeingAdded:Dictionary = new Dictionary();
   
    
    // properties ==============================================================   
    /**
    * Set the record
    * 
    * @param record The record 
    * */  
    public function set record(record:AGSGptRSSRecord):void  {
      if(this._record == null) {
        this._record = new AGSGptRSSRecord();
      } else {
        this._record = record;
        
      }
            
    }
    
    /**
    * Get the record
    * 
    * @return the record to be viewed
    * */ 
    private function get record():AGSGptRSSRecord {
      return this._record;
    }
    
    
    /**
    * Sets the end point
    * 
    * @param endPoint The endpoint object
    * */
    public function set endPoint(endPoint:AGSGptRSSEndPoint):void {
      this._endPoint = endPoint;
      
    }
    
    /**
    * Gets the end point
    * 
    * @return endPoint.  Possibly null.
    * */
    [Bindable]
    public function get endPoint():AGSGptRSSEndPoint {
      return this._endPoint;
    }
    
    /**
    * set Configuration XML.  Set once per instance always!
    * 
    * @param configXML
    * */
    public function set configXML(configXML:XML):void {
      if(configXML != null) { 
         _lblAddToMap = Utils.chkString(
           configXML.label.(@name=="lblAddToMap").@value);
      }
    }
    
    /**
    * Sets the main rss widget.  Set once per instance Always!
    * 
    * @param uiMainRssWidget the main rss instance 
    * */
    public function set uiMainRssWidget(uiMainRssWidget:AGSGptRSSWidget):void {
      this._uiMainRssWidget = uiMainRssWidget;
    }
    
    /**
    * Get the Rss widget 
    * 
    * @return instance of AGSGptRSSWidget
    * */
    private function get uiMainRssWidget():AGSGptRSSWidget {
      return this._uiMainRssWidget;
    }
    
    
    // methods =================================================================
    /**
    * Should be called after all the properties have been set
    * 
    * */
    public function init():void {
      
      showAddToMap();
      this.validateNow();
      
    }
    /**
    * Show add to map link
    * */
    private function showAddToMap():void {
      _bShowAddToMap = false;
      if(_record.serviceType == null) {
        AGSGptRSSWidget.agsGptRssModel.readService(_record, 
          showAddToMapCallback, endPoint);
         
      } else if(_record.mapServiceUrl != "" 
        && _record.serviceType != null) {
        var val:Object =  
          _dctMapsBeingAdded[getMapsBeingAddedKey(_record,endPoint)] ;
        var bAdding:Boolean =  (val != null);
        this.uiAddToMap(bAdding);  
        _bShowAddToMap = true;
      } 
    }
    
    /**
    * Call back from showAddToMap
    *
    * @param url The mapservice url
    * @param serviceType The service type
    * @param record the metadata record object 
    * */
    private function showAddToMapCallback(url:String, 
      serviceType:AGSGptEnumServiceTypes, id:String):void {
      if(serviceType == null || Utils.chkString(url) == "" 
        || this._record.id != id) {
        return;
      }
      var  record:AGSGptRSSRecord = this._record;
      record.serviceType = serviceType;
      record.mapServiceUrl = url 
      _bShowAddToMap = true;
      var val:Object =  
        _dctMapsBeingAdded[getMapsBeingAddedKey(record,endPoint)] ;
      var bAdding:Boolean =  (val != null);
      this.uiAddToMap(bAdding);
      
    }
    
    private static function getMapsBeingAddedKey(record:AGSGptRSSRecord,
      endPoint:AGSGptRSSEndPoint):String {
      return record.id + ":" + endPoint.siteUrl;
    }
    
    /**
    * Add To Map
    * */
    private function doAddToMap():void {
      if(_record == null || _record.id == null || _endPoint == null ||
        _endPoint.siteUrl == null) {
        return;
      }
      _dctMapsBeingAdded[getMapsBeingAddedKey(_record, _endPoint)] =
       new Boolean(true);
      uiAddToMap(true);
      _uiMainRssWidget.doAddToMap(_record, _endPoint)
    }
    
    /**
    * Notification of the map having been added
    * 
    * */
    public function doStopAddToMap(record:AGSGptRSSRecord,
      endPoint:AGSGptRSSEndPoint):void {
      if(record == null || record.id == null || endPoint == null 
         || endPoint.siteUrl == null || this.endPoint == null ||
         this.endPoint.siteName == null) {
      	return;
      }
      _dctMapsBeingAdded[getMapsBeingAddedKey(record, endPoint)] 
        = null;
        
      if(_record.id == record.id && this.endPoint.siteUrl == endPoint.siteUrl) {
        uiAddToMap(false);
      }
    }
    
    /**
    * User interface changes for Add to map 
    * 
    * @param adding is true if being added, false if not
    * */
    private function uiAddToMap(adding:Boolean):void {
      swfAddToMap.visible = adding;
      btnAddToMap.useHandCursor = adding;
      btnAddToMap.enabled = !adding;
      if(adding) {
        btnAddToMap.alpha = 0.3;
        swfAddToMap.alpha = 1;
      } else {
        btnAddToMap.alpha = 1;
        swfAddToMap.alpha = 0.3;
      }
      this.validateNow();
    
    }
    
             
  ]]>
</mx:Script>

<mx:HBox>
  <mx:SWFLoader id="swfLoading" 
     source="assets/images/loader.swf" 
     visible="false" />
  <mx:Text 
  styleName="WidgetTitle"
  text="{Utils.truncateString(_record.title, 50)}" 
  htmlText="true"/>
</mx:HBox>

<mx:TextArea 
  editable="false"
  width="{_width}"
  height="70"
  text="{Utils.truncateString(_record.abstract, 255)}" 
  htmlText="true"/>

<mx:Tile direction="horizontal" width="{_width}">
  <mx:Repeater id="rptLinks"  
    dataProvider="{AGSGptRSSWidget.agsGptRssModel.removeExcludedLinks(_record, 
      endPoint)}">
    <mx:LinkButton
      styleName="ControllerLinkButton"
      label="{rptLinks.currentItem.siteName}" 
      click="navigateToURL(new URLRequest(event.currentTarget.getRepeaterItem().siteUrl))"/>
  </mx:Repeater>
  <mx:Canvas visible="{btnAddToMap.visible}">
   <mx:LinkButton
      id="btnAddToMap" 
      styleName="ControllerLinkButton"
      label="{_lblAddToMap}" 
      visible="{_bShowAddToMap}"
      click="{this.doAddToMap()}"/>
    <mx:SWFLoader id="swfAddToMap" 
      source="assets/images/loader.swf" 
      visible="false" />
  </mx:Canvas>
 
</mx:Tile>
</mx:VBox>
